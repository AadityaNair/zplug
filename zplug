#!/bin/zsh

# environment variable (read-only)
typeset -gx ZPLUG_NAME=zplug
typeset -gx ZPLUG_VERSION=0.0.1
typeset -gx ZPLUG_RAWURL="https://raw.githubusercontent.com/b4b4r07/zplug/master/zplug"

# set default value if not present
typeset -gx ZPLUG_HOME=${ZPLUG_HOME:-~/zplug/repos}
typeset -gx ZPLUG_THREAD=${ZPLUG_THREAD:-16}

# global variable
typeset -g -A zplugs

# Disable job control
set +m

die() {
    echo "$@" >&2
}

bg_rotation_bar() {
    local current_count simbol
    for ((current_count=0; ; current_count++))
    do
        let simbol=current_count%4
        case "$simbol" in
            0) echo -ne " |\r";;
            1) echo -ne " /\r";;
            2) echo -ne " -\r";;
            3) echo -ne " \\\\\r";;
        esac
        sleep 0.05s
    done
}

zplug::list()
{
    local v
    local -A B
    for v in "${(@nO)zplugs}"; do
        B+=("${(@kv)zplugs[(eR)$v]}")
    done
    command printf '%-20s  =>  %s\n' "${(@kv)B}" \
        | perl -pe 's/^(.*)( *=>.*)$/\033[32m$1\033[m$2/g' \
        | perl -pe 's/(as|of|from|to|if|dir|file|at|do)(:)/\033[34m$1\033[m$2/g'
}

zplug::update()
{
    # Process argments
    local is_self
    case "$1" in
        --self)
            is_self=1
            ;;
    esac
    shift

    if [[ $is_self -eq 1 ]]; then
        return
    fi

    local cnt line
    cnt=0

    for line in "$@"; do
        # Run installation in subprocess
        {
            # All variables are treated as local variable
            # because of background job (subprocess)
            local -A zspec
            # zspec=( $(zplug::parser "$line") )
            zspec=( ${(@f)"$(zplug::parser "$line")"} )
            for k in ${(k)zspec}
            do
                if [[ $zspec[$k] == "-EMP-" ]]; then
                    zspec[$k]=""
                fi
            done

            # Reset timer
            typeset -F SECONDS=0

            if [[ $zspec[frozen] -eq 1 ]]; then
                command printf "\033[34mFrozen\033[m       $line\n"
                continue
            fi

            command printf "Updating...  $line\n"
            (){
                cd $zspec[dir]   || return 1
                git pull --quiet || return 1
            } 2>/dev/null

            if [[ $status -eq 0 ]]; then
                command printf "\033[32mUpdated!\033[m     %-20s\t(%.2fs)\n" "$line" $SECONDS
            else
                command printf "\033[31mNot Updated\033[m  %-20s\t(%.2fs)\n" "$line" $SECONDS
            fi
        } &
        # Prevent having too many subprocesses
        (( (cnt += 1) % ZPLUG_THREAD == 0 )) && wait
    done
    wait
}

zplug::install()
{
    local cnt line fail job
    local start end
    cnt=0
    [[ -d $ZPLUG_HOME ]] || mkdir -p $ZPLUG_HOME
    [[ $1 ]] || return

    start="$(date +%s)"
    for line in "$@"; do
        # Skip installed items
        [[ -e $ZPLUG_HOME/$line ]] &&
            {
                command printf "\033[31m-\033[m \033[32m${line}\033[m: already installed\n"
                continue
            }

        # Run installation in subprocess
        {
            # All variables are treated as local variable
            # because of background job (subprocess)
            local -A zspec
            # zspec=( $(zplug::parser "$line") )
            zspec=( ${(@f)"$(zplug::parser "$line")"} )
            for k in ${(k)zspec}
            do
                if [[ $zspec[$k] == "-EMP-" ]]; then
                    zspec[$k]=""
                fi
            done

            # Reset timer
            typeset -F SECONDS=0

            command printf "Installing...  $line\n"
            bg_rotation_bar & bg_pid=$!
            cd $ZPLUG_HOME

            # Download
            if [[ $zspec[from] == "gh-r" ]]; then

                # from GitHub Releases
                [[ -d ${zspec[name]%/*} ]] || mkdir -p "${zspec[name]%/*}"
                cd "${zspec[name]%/*}"
                eval 'L=$zspec[name] bash -c "$(curl -L git.io/releases)" -s os' >/dev/null 2>&1
                ret=$status

            else
                # from GitHub
                git clone \
                    --depth=1 \
                    --recursive \
                    --quiet \
                    -b $zspec[at] \
                    git@github.com:"$line" "$line" >/dev/null 2>&1
                ret=$status
            fi

            # If not empty
            if [[ $zspec[do] ]]; then
                if [[ $ret -eq 0 ]]; then
                    # Run command
                    eval "$zspec[do]"
                fi
            fi

            # Kill bg_rotation_bar animation
            kill -9 $bg_pid

            # Whether to success or not
            if [ $ret -eq 0 ]; then
                command printf "\033[32mInstalled!\033[m     %-20s\t(%.2fs)\n" "$line" $SECONDS
            else
                command printf "\033[31mNot Installed\033[m  %-20s\t(%.2fs)\n" "$line" $SECONDS
                false
            fi
        } &

        # Prevent having too many subprocesses
        (( (cnt += 1) % ZPLUG_THREAD == 0 )) && wait
    done

    # Count if fail
    fail=0

    # Pauses until execution of a background process has ended
    for job in ${${(v)jobstates##*:*:}%=*}
    do
        wait $job || let "fail+=1"
    done
    # Wait until all jobs known to the invoking shell have terminated
    wait

    # Skip footer prints if noting to do
    if [[ $cnt -eq 0 ]]; then
        return
    fi

    # Footer prints
    if [[ $fail -eq 0 ]]; then
        command printf "\033[;1m ==> Installed all plugins to $ZPLUG_HOME, successfully!\033[m\n"
    else
        command printf "\033[31;1m ==> Installed to $ZPLUG_HOME but failed.\033[m\n"
    fi

    end="$(date +%s)"
    command printf "$ZPLUG_NAME: total time %d sec\n" $(($end - $start))
}

zplug::parser()
{
    local zplug split_array
    zplug="$1"
    zplug="${zplug},${zplugs[$zplug]%,}"

    # Split by comma
    split_array=(${(s:,:)zplug})

    # Add spec variable
    # If value is empty, set -EMP- value to it
    local name as of to from if dir file at do frozen
    name="${split_array[1]}"
    as=${${${(M)split_array:#as*}#*:}:--EMP-}
    of=${${${(M)split_array:#of*}#*:}:--EMP-}
    to=${${${(M)split_array:#to*}#*:}:--EMP-}
    from=${${${(M)split_array:#from*}#*:}:--EMP-}
    if=${${${(M)split_array:#if*}#*:}:--EMP-}
    dir=${${${(M)split_array:#dir*}#*:}:--EMP-}
    file=${${${(M)split_array:#file*}#*:}:--EMP-}
    at=${${${(M)split_array:#at*}#*:}:-master}
    do=${${${(M)split_array:#do*}#*:}:--EMP-}
    frozen=${${${(M)split_array:#frozen*}#*:}:-0}

    local -A zplugs_parsed
    zplugs_parsed=(
      name    "$name"
      as      "$as"
      of      "$of"
      to      "$to"
      from    "$from"
      if      "$if"
      dir     "$dir"
      file    "$file"
      at      "$at"
      do      "$do"
      frozen  "$frozen"
    )

    local k
    for k in ${(k)zplugs_parsed}
    do
        echo "$k"
        echo "$zplugs_parsed[$k]"
    done
}

zplug::load()
{
    local f files k
    local zplug
    local -A zspec

    for zplug in ${(k)zplugs}
    do
        # Set zspec
        zspec=( ${(@f)"$(zplug::parser "$zplug")"} )
        # Replace -EMP- with ""(empty)
        for k in ${(k)zspec}
        do
            if [[ $zspec[$k] == "-EMP-" ]]; then
                zspec[$k]=""
            fi
        done

        # If not empty
        if [[ $zspec[if] ]]; then
            # Check if $spec[if] is true
            if ! eval '$zspec[if]' >/dev/null 2>&1; then
                die "$zspec[name]: (not load)"
                continue
            fi
        fi

        # Branch cmd/src
        case $zspec[as] in
            bin|cmd)
                files=()
                mkdir -p ${ZPLUG_HOME%/*}/bin 2>/dev/null

                # Search binary/command file
                if [[ -f $zspec[dir]/${zspec[name]#*/} ]]; then
                    files=("$zspec[dir]/${zspec[name]#*/}")
                elif [[ -f $zspec[dir]/$zspec[of] ]]; then
                    files+=("$zspec[dir]/$zspec[of]")
                elif [[ -f $zspec[dir]/$zspec[of]/${zspec[name]#*/} ]]; then
                    files+=("$zspec[dir]/$zspec[of]/${zspec[name]#*/}")
                elif [[ -f $zspec[dir] ]]; then
                    files+=("$zspec[dir]")
                fi

                # Make symlink to $ZPLUG_HOME/bin exported in parallel
                for f in "$files[@]"
                do
                    {
                        # Support commit sha
                        # local pat
                        # pat="^($(git branch | cut -c3- | paste -s -d"|" -))$"
                        # if [[ ! $zspec[at] =~ $pat ]]; then
                        #     cd $zspec[dir]
                        #     git checkout $zspec[at]
                        # fi
                        cd ${ZPLUG_HOME%/*}/bin
                        ln -snf "$f" ${zspec[file]:-.}
                    } &
                done
                wait

                # Export
                path=(${ZPLUG_HOME%/*}/bin $path)
                typeset -gx -U path
                ;;

            src|"")
                # Search plugin files by pattern
                files=($(zsh -c "echo $zspec[dir]/${zspec[of]:-*.sh(N)}" 2>/dev/null))
                for f in $files
                do
                    source "$f"
                done
                ;;

            debug)
                # Debug mode
                # nothing to do
                ;;
        esac
    done
}

zplug() {
    local zplug
    zplug="$1"
    shift # Shift "username/reponame",

    # $zplug consists of "username/reponame", specifier:value, ...
    case "$zplug" in

        update)
            zplug::update "$@"
            return $status
            ;;

        list)
            zplug::list
            return $status
            ;;

        i|install)
            zplug::install ${(k)zplugs}
            return $status
            ;;

        l|load)
            zplug::load
            return $status
            ;;

        "")
            return 1
            ;;

        *)
            if [[ $zplug =~ ^[A-Za-z0-9._-]+/[A-Za-z0-9._-]+,?$ ]]; then
                local i specifier
                for i in "$@"
                do
                    case "${i%:*}" in
                        as|of|from|to|if|dir|file|at|do|frozen)
                            specifier="${specifier}${i}"
                            ;;
                        *)
                            die "$i: invalid specifier"
                            return 1
                            ;;
                    esac
                done

                # Trim last comma
                zplug=${zplug%,}
                specifier=${specifier%,}
                # Add to $zplugs(AA; key:"user/repo" => value:"specifier:value")
                zplugs+=($zplug "${specifier:-}")
                # Add dir specifier to $zplugs
                zplugs[$zplug]+="${zplugs[$zplug]:+,}dir:$ZPLUG_HOME/$zplug"
            fi
            ;;
    esac
}

#
# *************** DEBUG ***************
#

zplug "k4rthik/git-cal", as:cmd #, do:"echo installed"
zplug "b4b4r07/zsh-gomi", as:cmd, of:bin/gomi, frozen:1
zplug "b4b4r07/ssh-keyreg", as:cmd, of:bin
zplug "b4b4r07/cli-finder", as:cmd, of:bin
zplug "b4b4r07/tmuxlogger"
zplug "b4b4r07/enhancd", at:v1
zplug "hchbaw/opp.zsh", if:"((${ZSH_VERSION//./} < 508))"
zplug "b4b4r07/http_code", \
    as:cmd, \
    of:bin
zplug "holman/spark", as:cmd
zplug "junegunn/fzf-bin", \
    as:bin, \
    from:gh-r, \
    file:fzf

# zplug "stedolan/jq" \
#     as:bin, \
#     from:gh-r \
#     | zplug "b4b4r07/emoji-cli"

# zplug list
# zplug install
# zplug load
zplug update --self

export PATH=/bin:/usr/bin:/usr/local/bin
