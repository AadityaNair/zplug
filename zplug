#!/bin/zsh

die() {
    echo "$@" >&2
}

# Disable job control
set +m

# environment variable (read-only)
typeset -gx ZPLUG_NAME=zplug
typeset -gx ZPLUG_VERSION=0.0.1

# set default value if not present
typeset -gx ZPLUG_HOME=${ZPLUG_HOME:-~/zplug}
typeset -gx ZPLUG_THREAD=${ZPLUG_THREAD:-16}

# global variable
typeset -g -A zplugs

bg_rotation_bar() {
    local current_count simbol
    for ((current_count=0; ; current_count++))
    do
        let simbol=current_count%4
        case "$simbol" in
            0) echo -ne " |\r";;
            1) echo -ne " /\r";;
            2) echo -ne " -\r";;
            3) echo -ne " \\\\\r";;
        esac
        sleep 0.05s
    done
}

zplug::list()
{
    local v
    local -A B
    for v in "${(@nO)zplugs}"; do
        B+=("${(@kv)zplugs[(eR)$v]}")
    done
    printf '%-20s => %s\n' "${(@kv)B}"
}

zplug::install()
{
    # prepare
    local cnt line fail job
    cnt=0
    [[ -d $ZPLUG_HOME ]] || mkdir -p $ZPLUG_HOME
    [[ $1 ]] || return

    for line in "$@"; do
        {
            local -A zspec
            zspec=( $(zplug::parser $line) )
            for k in ${(k)zspec}
            do
                if [[ $zspec[$k] == "-EMP-" ]]; then
                    zspec[$k]=""
                fi
            done

            SECONDS=0
            printf "Installing...  $line\n"
            bg_rotation_bar & bg_pid=$!
            cd $ZPLUG_HOME

            local -A zspec
            zspec=( $(zplug::parser $line) )
            if [[ $zspec[from] == "gh-r" ]]; then
                eval 'L=$zspec[name] bash -c "$(curl -L git.io/releases)" -s os' >/dev/null 2>&1
                ret=$status
            else
                git clone \
                    --depth=1 \
                    --recursive \
                    --quiet \
                    -b $zspec[at] \
                    git@github.com:"$line" >/dev/null 2>&1
                ret=$status
            fi
            kill -9 $bg_pid
            if [ $ret -eq 0 ]; then
                printf "\e[32mInstalled!\e[m     $line \t ($SECONDS sec.)\n"
            else
                printf "\e[31mNot Installed\e[m  $line\n"
                false
            fi
        } &
        (( (cnt += 1) % 16 == 0 )) && wait
    done

    fail=0
    typeset -F SECONDS=0

    # wait
    for job in ${${(v)jobstates##*:*:}%=*}
    do
        wait $job || let "fail+=1"
    done
    wait

    if [[ "$fail" -eq 0 ]]; then
        printf "\e[;1m ==> Installed all plugins to $ZPLUG_HOME, successfully!\e[m\n"
    else
        printf "\e[31;1m ==> Installed to $ZPLUG_HOME but failed.\e[m\n"
    fi
    printf "$ZPLUG_NAME: total time %.2f sec\n" $SECONDS
}

zplug::parser()
{
    local zplug split_array
    zplug="$1"
    zplug="${zplug},${zplugs[$zplug]%,}"

    split_array=(${(s:,:)zplug})

    local name as of to from _if dir file at
    name="${split_array[1]}"
    as=${${${(M)split_array:#as*}#*:}:--EMP-}
    of=${${${(M)split_array:#of*}#*:}:--EMP-}
    to=${${${(M)split_array:#to*}#*:}:--EMP-}
    from=${${${(M)split_array:#from*}#*:}:--EMP-}
    _if=${${${(M)split_array:#if*}#*:}:--EMP-}
    dir=${${${(M)split_array:#dir*}#*:}:--EMP-}
    file=${${${(M)split_array:#file*}#*:}:--EMP-}
    at=${${${(M)split_array:#at*}#*:}:-master}

    local -A zplugs_parsed
    zplugs_parsed=(
      name "$name"
      as   "$as"
      of   "$of"
      to   "$to"
      from "$from"
      if   "$_if"
      dir  "$dir"
      file "$file"
      at   "$at"
    )

    echo "${(kv)zplugs_parsed}"
}

zplug::load()
{
    local load_path f files

    local -A zspec
    local zplug
    for zplug in ${(k)zplugs}
    do
        zspec=( $(zplug::parser $zplug) )
        for k in ${(k)zspec}
        do
            if [[ $zspec[$k] == "-EMP-" ]]; then
                zspec[$k]=""
            fi
        done

        if [[ $zspec[if] ]]; then
            if ! eval "$zspec[if] >/dev/null 2>&1"; then
                die "$zspec[name]: (not load)"
                continue
            fi
        fi

        case $zspec[as] in
            bin|cmd)
                mkdir -p $ZPLUG_HOME/{repos,bin} 2>/dev/null
                for f in {$ZPLUG_HOME/,$zspec[dir]/${zspec[of]:+"$zspec[of]/"}${zspec[name]#*/}}*(.f755N)
                do
                    {
                        # Support commit sha
                        # local pat
                        # pat="^($(git branch | cut -c3- | paste -s -d"|" -))$"
                        # if [[ ! $zspec[at] =~ $pat ]]; then
                        #     cd $zspec[dir]
                        #     git checkout $zspec[at]
                        # fi
                        cd $ZPLUG_HOME/bin
                        ln -snf "$f" ${zspec[file]:-.}
                    } &
                done
                wait
                path=($ZPLUG_HOME/bin $path)
                typeset -gx -U path
                ;;
            src|"")
                files=($(zsh -c "echo $zspec[dir]/${zspec[of]:-*.sh(N)}" 2>/dev/null))
                for f in $files
                do
                    source "$f"
                done
                ;;
            debug)
                ;;
        esac
    done
}

zplug() {
    local zplug
    zplug="$1"
    case "$zplug" in
        list)
            zplug::list
            return $status
            ;;
        i|install)
            zplug::install ${(k)zplugs}
            return $status
            ;;
        l|load)
            zplug::load
            return $status
            ;;
        "")
            die
            return 1
            ;;
        *)
            shift
            if [[ $zplug =~ ^[A-Za-z0-9._-]+/[A-Za-z0-9._-]+,?$ ]]; then
                local i specifier
                for i in "$@"
                do
                    case "${i%:*}" in
                        as|of|from|to|if|dir|file|at)
                            specifier="${specifier}${i}"
                            ;;
                        *)
                            die "$i: invalid specifier"
                            return 1
                            ;;
                    esac
                done
                # trim last comma
                zplug=${zplug%,}
                specifier=${specifier%,}

                zplugs+=($zplug "${specifier:-}")
                zplugs[$zplug]+="${zplugs[$zplug]:+,}dir:$ZPLUG_HOME/${zplug#*/}"
            fi
            ;;
    esac
}

zplug "k4rthik/git-cal", as:cmd
zplug "b4b4r07/enhancd", of:"zsh/*.zsh"
zplug "hchbaw/opp.zsh", if:"[ ${ZSH_VERSION%%.*} -lt 5 ]"
zplug "b4b4r07/http_code", \
    as:cmd, \
    of:bin
zplug "holman/spark", as:cmd
zplug "b4b4r07/emoji-cli", of:"*.zsh"
zplug "junegunn/fzf-bin", \
    as:bin, \
    from:gh-r, \
    file:fzf

# zplug "stedolan/jq" \
#     as:bin, \
#     from:gh-r \
#     | zplug "b4b4r07/emoji-cli"

# zplug list

zplug install
zplug load
