#!/bin/zsh

__import "core/core"
__import "print/print"
__import "job/spinner"

local    is_verbose=false
local    arg line
local -a args fail
local -A zspec

while (( $# > 0 ))
do
    arg="$1"
    case "$arg" in
        --verbose)
            is_verbose=true
            ;;
        -*|--*)
            __die "[zplug] $arg: Unknown option\n"
            return 1
            ;;
        *)
            args+=("$arg")
            ;;
    esac
    shift
done

for line in ${(u)${args[@]:gs:@::}}
do
    zspec=( ${(@f)"$(__parser__ "$line")"} )
    case "$zspec[from]" in
        "local")
            continue
            ;;
        "oh-my-zsh")
            if [[ ! -d ${zspec[dir]:h} ]]; then
                fail+=("$zspec[name]")
            fi
            ;;
        *)
            if [[ ! -d $zspec[dir] ]]; then
                fail+=("$zspec[name]")
            fi
            ;;
    esac
done

if (( $#fail > 0 )); then
    if $is_verbose; then
        __put "- $fg[red]%s$reset_color: not installed\n" "${(@nO)fail}"
    fi
    return 1
else
    return 0
fi
