#!/bin/zsh

__import "core/core"
__import "print/print"
__import "job/spinner"

local    is_verbose=false
local    arg line
local -a args fail
local -A zspec

while (( $# > 0 ))
do
    arg="$1"
    case "$arg" in
        --verbose)
            is_verbose=true
            ;;
        -*|--*)
            __die "[zplug] $arg: Unknown option\n"
            return 1
            ;;
        *)
            args+=("$arg")
            ;;
    esac
    shift
done

for line in ${${args[@]:-${(k)zplugs[@]}}:gs:@::}
do
    zspec=( ${(@f)"$(__parser__ "$line")"} )
    case "$zspec[from]" in
        "local")
            continue
            ;;
        *)
            if __is_callback_defined "$zspec[from]" check; then
                __use_external_source "$zspec[from]" check "$line"

                if (( $status )); then
                    fail+=("$zspec[name]")
                fi
            else
                if [[ ! -d $zspec[dir] ]]; then
                    fail+=("$zspec[name]")
                fi
            fi
            ;;
    esac
done

if (( $#fail > 0 )); then
    if $is_verbose; then
        __put "- $fg[red]%s$reset_color: not installed\n" "${(@nO)fail}"
    fi
    return 1
else
    return 0
fi
